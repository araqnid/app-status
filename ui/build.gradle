task npmInstall(type: Exec) {
    inputs.file project.file("package.json")
    outputs.files fileTree(project.file("node_modules")) { include '**/package.json' }
    commandLine "npm", "install"
}

task webpack(type: Exec, dependsOn: npmInstall) {
    outputs.files fileTree(project.file("build/site")) {
        include 'bundle.js'
    }
    inputs.files fileTree(project.file("src/main/js")) {
        include '**/*.js'
        include '**/*.jsx'
    }
    inputs.files project.file('webpack.config.js')
    commandLine "node_modules/.bin/webpack", "-p", "--devtool", "source-map"
}

task clean {
    doLast {
        delete project.file("node_modules")
        delete project.file("build")
    }
}

task staticFiles(type: Copy) {
    from 'src/main/web'
    into 'build/site'
}

task site(dependsOn: [webpack, staticFiles]) {
}

task build(dependsOn: site) {
}
