task npmInstall(type: Exec) {
    inputs.file project.file("package.json")
    outputs.files fileTree(project.file("node_modules")) { include '**/package.json' }
    commandLine "npm", "install"
}

task webpack(type: Exec, dependsOn: npmInstall) {
    inputs.files project.file("node_modules/.bin/webpack")
    inputs.files fileTree(project.file("src/main/web")) {
        include '**/*.js'
        include '**/*.jsx'
    }
    inputs.files project.file('webpack.config.js')
    outputs.files project.file("build/site/bundle.js")
    outputs.files project.file("build/site/bundle.js.map")
    outputs.files project.file("build/site/index.html")
    environment("NODE_ENV", "production")
    commandLine "node_modules/.bin/webpack", "-p", "--devtool", "source-map"
}

task clean {
    doLast {
        delete project.file("node_modules")
        delete project.file("build")
    }
}

task site(dependsOn: [webpack]) {
}

task build(dependsOn: [site]) {
}
